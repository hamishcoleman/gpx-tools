#!/usr/bin/env perl
use warnings;
use strict;
#
# Process the given files and output one big merged gpx
#

use File::Spec;

# allow the libs to be in the bin dir
use FindBin;
use lib File::Spec->catdir($FindBin::RealBin,"lib");

use XML::Twig;
use IO::File;

use SplitList::Time;
use GPX::Files;

use HC::Common;
my $option = {
};
my @option_list = (
    "splitlist=s",
    "filter|f=s",
);
HC::Common::do_options($option,@option_list);
exit if (defined($option->{help}));

if (!defined($option->{splitlist})) {
    $option->{splitlist} = shift @ARGV;
}

my $splitlist = SplitList::Time->new();
my $split_fh = IO::File->new($option->{splitlist}, "r");
$splitlist->parse_fd($split_fh);

my $gpxfiles = GPX::Files->new();
$gpxfiles->set_splitlist($splitlist);

if (defined($option->{filter})) {
    $gpxfiles->set_filter2console($option->{filter});
}

for my $file (@ARGV) {
    printf(STDERR "file: %s\n",$file);

    my $fh;
    if ( $file =~ m/\.gz$/ ) {
        $fh = IO::File->new("gzip -dc <'$file' |");
    } elsif ( $file =~ m/\.xz$/ ) {
        $fh = IO::File->new("xz -dc <'$file' |");
    } else {
        $fh = IO::File->new($file,"r");
    }
    if (!defined $fh) {
        die "could not open $file: $!";
    }

    my $twig = XML::Twig->new(
            start_tag_handlers => {
                trkseg => sub {$gpxfiles->add_trkseg()},
            },
            twig_handlers => {
                'trk/name' => sub {$gpxfiles->add_trk_name($_[1])},
                trkpt => sub {
                    $gpxfiles->add_trkpt($_[1]);
                    $_[1]->purge();
                },
            },
    );
    $twig->parse($fh);
    # TODO:
    # - something less verbose - but still fatal - if we fail to read the xml
}

$gpxfiles->flush();
